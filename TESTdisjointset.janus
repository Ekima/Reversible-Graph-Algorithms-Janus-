/*
 * Disjoint set data structure.
 * Has three functions: makeset, union and checkset (similar to findset)
 * Uses a linked list with a weighted-union heuristic, thus hopefully
 * maintaining O(m + nlgn) time, where m is a sequence of the three functions
 * and n is he amount of makeset operations.
 * The data structure is maintained by two two-dimensional array,
 * containing node information (next node, which set it belongs to), and set
 * information (head, tail and length).
 */

/*
 * makeset creates a node for a single element and adds it to a new set.
 *
 * Parameters:
 * int x            Numbered element to be added - Should NOT be in the list
 *                  currently.
 * int nodes[]      A list of all current nodes added to the data structure. 
 * int sets[]       A list of all current sets added to the data structure.
 *
 * Postcondition:
 * A disjoint set data structure with the added element
 */
procedure makeset(int x, int nodes[], int sets[])
    nodes[x][0] -= 1 // Next element, initially points to no element
    nodes[x][1] += x // Part of its own set
    sets[x][0] += x // Head
    sets[x][1] += x // Tail
    sets[x][2] += 1 // Length

/*
 * searchnodes traverses through a linked list and changes the set membership
 *
 * Parameters:
 * int i            Current node
 * int newset       The new set
 * int oldset       The current set
 * int nodes[]      A list of all current nodes added to the data structure. 
 * int sets[]       A list of all current sets added to the data structure.
 *
 * Postcondition:
 * A disjoint set data structure with the added element
 */
procedure searchnodes(int i, int newset, int oldset, int nodes[], int sets[])
    nodes[i][1] -= oldset
    nodes[i][1] += newset
    if nodes[i][0] != -1 then
        call searchnodes(nodes[i][0], newset, oldset, nodes, sets)
    fi nodes[i][0] != -1

/*
 * union combines two sets.
 *
 * Parameters:
 * int x            First set
 * int y            Second set
 * int nodes[]      A list of all current nodes added to the data structure. 
 * int sets[]       A list of all current sets added to the data structure.
 *
 * Postcondition:
 * The combined set of x and y are added to the data structure
 */
procedure union(int x, int y, int nodes[], int sets[],
                int garbage[], int counter)
    if sets[nodes[y][1]][2] = 0 || sets[nodes[x][1]][2] = 0 then
        error("Trying to perform union with an empty set")
    fi sets[nodes[y][1]][2] = 0 || sets[nodes[x][1]][2] = 0
    
    // Calculate which set is added to the other
    local int z = 0
    if sets[nodes[x][1]][2] >= sets[nodes[y][1]][2] then
        garbage[counter][3] += y
        z += x
    else
        garbage[counter][3] += x
        z += y
    fi sets[nodes[x][1]][2] >= sets[nodes[y][1]][2]
    
    // Store garbage
    // Tail of first set
    garbage[counter][0] += sets[nodes[garbage[counter][3]][1]][1]
    garbage[counter][1] += sets[nodes[z][1]][0] // Head of second set
    garbage[counter][2] += sets[nodes[z][1]][2] // Length of second set
    
    // Set the tail of the first set to point to the head of the second set
    nodes[garbage[counter][3]][0] += garbage[counter][1] + 1
    
    // Set the tail of the first set to the tail of the second set
    sets[nodes[garbage[counter][3]][1]][1] -= garbage[counter][0]
    sets[nodes[garbage[counter][3]][1]][1] += sets[nodes[z][1]][1]
    
    // Set all values in the second set to 0
    sets[nodes[z][1]][0] -= garbage[counter][1]
    sets[nodes[z][1]][1] -= sets[nodes[garbage[counter][3]][1]][1]
    sets[nodes[z][1]][2] -= garbage[counter][2]
    
    call searchnodes(garbage[counter][1], nodes[garbage[counter][3]][1],
                     garbage[counter][1], nodes, sets)
    
    // Add the length of the second set to the length of the first set
    sets[nodes[garbage[counter][3]][1]][2] += garbage[counter][2]
    
    if garbage[counter][3] = x then
        z -= y
    else
        z -= x
    fi garbage[counter][3] = x
    delocal int z = 0
    counter += 1
    
/*
 * checksets checks if two nodes are in the same set.
 *
 * Parameters:
 * int x            First node
 * int y            Second node
 * int nodes[]      A list of all current nodes added to the data structure. 
 * int found        A truth value, must initially be 0
 *
 * Postcondition:
 * found = 1 if the nodes are in the same set, found = 0 if they are not
 */
procedure checksets(int x, int y, int nodes[], int found)
    if nodes[x][1] = nodes[y][1] then
        found += 1
    fi nodes[x][1] = nodes[y][1]

/*
 * Main procedure. Tests the functions.
 */
procedure main()
    // Node information (empty). Can contain up to 5 nodes.
    int nodes[5][2] = {{0}}
    // Set information (empty). Contains the sets expected at output, here 1.
    int set[3]
    // Garbage output
    int garbage[size(nodes)-1][4] = {{0}}
    // Intermediate garbage (all set information and garbage counter)
    local int sets[size(nodes)][3] = {{0}}, int counter = 0
    
    // Initialize the a full collection
    local int i = 0
    from i = 0 do
        call makeset(i, nodes, sets)
        i += 1
    until i = size(nodes)
    delocal int i = size(nodes)
    
    local int found = 0
    call checksets(0, 1, nodes, found)
    if found = 0 then
        printf("Node 1 and 2 are not in the same set")
    else
        printf("Node 1 and 2 are in the same set")
    fi found = 0
    uncall checksets(0, 1, nodes, found)
    delocal int found = 0
    
    call union(0, 1, nodes, sets, garbage, counter)
    local int found = 0
    call checksets(0, 1, nodes, found)
    if found = 0 then
        printf("Node 1 and 2 are not in the same set")
    else
        printf("Node 1 and 2 are in the same set")
    fi found = 0
    uncall checksets(0, 1, nodes, found)
    delocal int found = 0
    
    call union(3, 4, nodes, sets, garbage, counter)
    local int found = 0
    call checksets(3, 4, nodes, found)
    if found = 0 then
        printf("Node 4 and 5 are not in the same set")
    else
        printf("Node 4 and 5 are in the same set")
    fi found = 0
    uncall checksets(3, 4, nodes, found)
    delocal int found = 0
    
    call union(2, 4, nodes, sets, garbage, counter)
    local int found = 0
    call checksets(2, 4, nodes, found)
    if found = 0 then
        printf("Node 3 and 5 are not in the same set")
    else
        printf("Node 3 and 5 are in the same set")
    fi found = 0
    uncall checksets(2, 4, nodes, found)
    delocal int found = 0
    
    call union(4, 1, nodes, sets, garbage, counter)
    local int found = 0
    call checksets(4, 1, nodes, found)
    if found = 0 then
        printf("Node 5 and 2 are not in the same set")
    else
        printf("Node 5 and 2 are in the same set")
    fi found = 0
    uncall checksets(4, 1, nodes, found)
    delocal int found = 0
    sets[nodes[0][1]][0] <=> set[0]
    sets[nodes[0][1]][1] <=> set[1]
    sets[nodes[0][1]][2] <=> set[2]
    delocal int sets[size(nodes)][3] = {{0}}, int counter = size(nodes)-1