/*
 * Depth First Search
 * 
 * Takes an unweighted graph represented as an adjacency list.
 * Timestamp info is stored in a separate twodimensional array.
 * Returns the original graph with a list of timestamps represented the
 * trees in the graph.
 *
 * The adjacency list implementation is described in adjacencylist.janus
 */
 
 /*
 * getlist gets the contents of a single list in the adjacency list.
 *
 * Parameters:
 * int adjacent[]  A list of adjacency list elements
 * int start       A pointer to the start of the list to be extracted
 * int end         A pointer to the end of the list to be extracted
 * int res[]       A list to store the returned elements in
 * 
 * Returns:
 * A copy of a the items in a list in the adjacency list.
 */
 
procedure getlist(int adjacent[], int start, int end, int res[])
    // Elements of the wanted list are loaded into res
    local int r = start
    from r = start do
        res[r - start] += adjacent[r]
        r += 1
    until r = end
    delocal int r = end

/*
 * DFS initializes the depth first search of a graph G at time 0.
 *
 * Parameters:
 * int G[] A list of adjacency list elements
 * int nodes[]    Information about the nodes (pointers and (empty) timestamps)
 *
 * Returns:
 * The original graph represented by G and the pointers in nodes, with an
 * updated list of timestamps representing the subtrees of G
 */
procedure DFS(int G[], int nodes[])
    local int time = 0, int i = 0
    
    // We run through each individual node and check if they have been visited
    // (timestamped). If they haven't we run DFSvisit.
    from i = 0 do
        if nodes[i][1] = 0 then
            call DFSvisit(G, nodes, i, time)
        fi nodes[i][2] = time
        i += 1
    until i = size(nodes)
    
    delocal int time = size(nodes) * 2, int i = size(nodes)

/*
 * DFSvisit marks the current node with the current time, then recursively
 * does the same with each of its adjacent nodes if they have not been visited.
 *
 * Parameters:
 * int G[] A list of adjacency list elements
 * int nodes[]    Information about the nodes (pointers and (empty) timestamps)
 * int u           A number indicating the current node visited
 * int time        The current time
 *
 * Returns:
 * A timestamped tree of all the subnotes in u
 */
procedure DFSvisit(int G[], int nodes[], int u, int time)
    // The time is incremented and the node gets entry-timestamped
    time += 1
    nodes[u][1] += time
    
    // end contains a pointer to the end of the wanted list
    local int end = 0
    if u = size(nodes) - 1 then
        // If we try to get the last list we stop at the end of the element list
        end += size(G)
    else
        // Or else, we stop where the next list starts
        end += nodes[u + 1][0]
    fi u = size(nodes) - 1
    
    // The adjacent nodes are checked and if they have not been visited,
    // DFSvisit is called with the unvisited node
    local int r = nodes[u][0]
    from r = nodes[u][0] do
        if nodes[G[r]-1][1] = 0 then
            call DFSvisit(G, nodes, G[r]-1, time)
        fi nodes[G[r]-1][1] = time || nodes[G[r]-1][2] = time
        r += 1
    until r = end
    delocal int r = end
    
    // The time is incremented and the node gets exit-timestamped
    time += 1
    nodes[u][2] += time
    
    // end is delocated
    if u = size(nodes) - 1 then
        end -= size(G)
    else
        end -= nodes[u + 1][0]
    fi u = size(nodes) - 1
    delocal int end = 0

/*
 * Main procedure. Creates an adjacency list and runs DFS on it.
 */
procedure main()
    // Adjacency list, list of adjacent nodes
    int G[] = {2, 4, 1, 5, 4, 1, 3, 5, 2, 4, 7, 4}
    // Adjacency list, node information.
    // Each node has a pointer to their first element and two timestamps
    int nodes[7][3] = {{0}, {2}, {4}, {5}, {8}, {10}, {11}}
    
    call DFS(G, nodes)
